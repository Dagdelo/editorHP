%%HP: T(3)A(R)F(.);
\<<
"O livro vai aqui"
CLLCD "Abrindo..." 1. DISP
STR\->
DIR
  MONTARINDICE
  \<< 0. 0. { } \-> subindice n lista
    \<< \<-indice 'subindice' STO \<-posIndice OBJ\-> 1. SWAP
      START subindice SWAP GET 'subindice' STO
      NEXT subindice OBJ\-> 2. - 'n' STO
      IF
      THEN \<-paginas SWAP GET SWAP DROP COMPILARPAGINA SUBIRINDICE
      ELSE 1. n
        FOR j 1. GET n 2. + j - 2. \->LIST 1. \->LIST 'lista' STO+
        NEXT { { ".." 0. } } 'lista' STO+
        IF lista 2. CLLCD CHOOSE
        THEN
          IF DUP
          THEN '\<-posIndice' STO+ MONTARINDICE
          ELSE DROP SUBIRINDICE
          END
        END
      END
    \>>
  \>>
  MONTARANEXOS
  \<<
    IF "Anexos" \<-anexos 0. CLLCD CHOOSE
    THEN
      IFERR STR\->
      THEN "Formato inv\225lido" MSGBOX
      END
    ELSE MONTARINDICERAIZ
    END
  \>>
  COMPILARPAGINA
  \<< 0. 131. 0. 0. 0. \-> \<-n \<-largura tipo \<-altura \<-y
    \<< OBJ\-> '\<-n' STO 1. \<-n
      START OBJ\-> DROP 'tipo' STO
        CASE tipo 0. ==
          THEN PREOBJTXT
          END tipo 1. ==
          THEN PREOBJEQ
          END tipo 2. ==
          THEN PREOBJIMG
          END tipo 3. ==
          THEN PREOBJCABECALHO
          END tipo 4. ==
          THEN PREOBJREGUA
          END
        END
      NEXT \<-largura R\->B \<-altura R\->B BLANK PICT STO 0. \<-n 1. -
      FOR j \<-n j - ROLL OBJ\-> DROP 'tipo' STO
        CASE tipo 0. ==
          THEN 0. 0. \-> lado img w h
            \<< img SIZE B\->R 'h' STO B\->R 'w' STO
              CASE lado -1. ==
                THEN 0.
                END lado 0. ==
                THEN \<-largura w - 2. / FLOOR
                END lado 1. ==
                THEN \<-largura w -
                END
              END R\->B \<-y R\->B 2. \->LIST PICT SWAP img GOR '\<-y' h 8. + STO+
            \>>
          END tipo 1. ==
          THEN \-> cor h
            \<< \<-largura R\->B h R\->B BLANK
              IF cor
              THEN NEG
              END PICT SWAP 0. R\->B \<-y R\->B 2. \->LIST SWAP GOR '\<-y' h 8. + STO+
            \>>
          END
        END
      NEXT { } PVIEW
    \>>
  \>>
  BUSCAR
  \<< { } \-> string opcoes
    \<< 1. \<-strings SIZE
      FOR j
        IF '\<-strings' j GET string POS
        THEN 'opcoes' "P\225gina " j + j 2. \->LIST 1. \->LIST STO+
        END
      NEXT
      IF opcoes SIZE
      THEN
        WHILE "Resultados" opcoes 1. CLLCD CHOOSE
        REPEAT '\<-paginas' SWAP GET COMPILARPAGINA
        END
      ELSE "Nada encontrado" MSGBOX
      END
    \>>
  \>>
  ABRIRLIVRO
  \<< OBJ\-> DROP 0. { } { } \-> versao \<-nome opcoes \<-indice \<-paginas \<-anexos \<-strings n lista \<-posIndice
    \<<
      IF versao 1. \=/
      THEN "Vers\227o incompat\237vel" DOERR
      END MONTARINDICERAIZ
    \>>
  \>>
  SUBIRINDICE
  \<< \<-posIndice TAIL DUP '\<-posIndice' STO
    IF SIZE
    THEN MONTARINDICE
    ELSE MONTARINDICERAIZ
    END
  \>>
  PREOBJTXT
  \<< 2. \->GROB DUP SIZE B\->R 8. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 0. 3. \->LIST \<-n ROLLD
  \>>
  PREOBJREGUA
  \<< DUP 8. + '\<-altura' STO+ 1. 3. \->LIST \<-n ROLLD
  \>>
  PREOBJIMG
  \<< DUP SIZE B\->R 8. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 0. 3. \->LIST \<-n ROLLD
  \>>
  PREOBJEQ
  \<<
    IFERR STR\->
    THEN DROP "[\019\002\019Erro na equa\231\227o\019\002\019]" 2. \->GROB
    ELSE 0. \->GROB
    END DUP SIZE B\->R 8. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 0. 3. \->LIST \<-n ROLLD
  \>>
  PREOBJCABECALHO
  \<< 2. \->GROB DUP SIZE B\->R 8. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO SWAP DROP 0. 3. \->LIST \<-n ROLLD
  \>>
  MONTARINDICERAIZ
  \<< \<-indice 1.
    \<< 1. GET NSUB 2. \->LIST
    \>> DOSUBS
    IF \<-anexos SIZE
    THEN { { "Anexos" 0. } } +
    END { { "Buscar" -1. } } + \<-nome SWAP 1.
    IF CLLCD CHOOSE
    THEN
      IF DUP -1. ==
      THEN DROP "Digite o termo
a ser buscado" ""
        IFERR INPUT
        THEN DROP DROP
        ELSE BUSCAR
        END MONTARINDICERAIZ
      ELSE
        IF DUP
        THEN '\<-posIndice' STO+ MONTARINDICE
        ELSE DROP MONTARANEXOS
        END
      END
    END
  \>>
END
'TEMPLIVRO' PGDIR
'TEMPLIVRO' STO
TEMPLIVRO
ABRIRLIVRO
UPDIR
'TEMPLIVRO' PGDIR
\>>