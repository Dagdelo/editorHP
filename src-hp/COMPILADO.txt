%%HP: T(3)A(R)F(.);
\<<
"O livro vai aqui"
CLLCD "Abrindo..." 1. DISP
STR\->
DIR
  MONTARINDICERAIZ
  \<< \<-indice 1.
    \<< 1. GET NSUB 2. \->LIST
    \>> DOSUBS
    IF \<-anexos SIZE
    THEN { { "Anexos" 0. } } +
    END { { "Buscar" -1. } } + \<-nome SWAP 1.
    IF CLLCD CHOOSE
    THEN
      IF DUP -1. ==
      THEN DROP "Digite o termo
a ser buscado" ""
        IFERR INPUT
        THEN DROP DROP
        ELSE BUSCAR
        END MONTARINDICERAIZ
      ELSE
        IF DUP
        THEN '\<-posIndice' STO+ MONTARINDICE
        ELSE DROP MONTARANEXOS
        END
      END
    END
  \>>
  MONTARINDICE
  \<< 0. 0. { } \-> subindice n lista
    \<< \<-indice 'subindice' STO \<-posIndice OBJ\-> 1. SWAP
      START subindice SWAP GET 'subindice' STO
      NEXT subindice OBJ\-> 2. - 'n' STO
      IF
      THEN SWAP DROP GETPAGINACOMPILADA SUBIRINDICE
      ELSE 1. n
        FOR j 1. GET n 2. + j - 2. \->LIST 1. \->LIST 'lista' STO+
        NEXT { { ".." 0. } } 'lista' STO+
        IF lista 2. CLLCD CHOOSE
        THEN
          IF DUP
          THEN '\<-posIndice' STO+ MONTARINDICE
          ELSE DROP SUBIRINDICE
          END
        END
      END
    \>>
  \>>
  MONTARANEXOS
  \<<
    IF "Anexos" \<-anexos 0. CLLCD CHOOSE
    THEN
      IFERR STR\->
      THEN "Formato inv\225lido" MSGBOX
      END
    ELSE MONTARINDICERAIZ
    END
  \>>
  GETPAGINACOMPILADA
  \<< '\<-cache' OVER GET
    IF DUP 0. ==
    THEN DROP '\<-paginas' OVER GET COMPILARPAGINA PICT RCL DUP ROT SWAP '\<-cache' UNROT PUT
    ELSE SWAP DROP
    END PICT STO { } PVIEW
  \>>
  COMPILARPAGINA
  \<< 0. 131. 0. 0. 0. \-> \<-n \<-largura tipo \<-altura \<-y
    \<< OBJ\-> '\<-n' STO "Gerando..." 1. DISP 1. \<-n
      FOR j OBJ\-> DROP 'tipo' STO
        CASE tipo 0. ==
          THEN PREOBJTXT
          END tipo 1. ==
          THEN PREOBJEQ
          END tipo 2. ==
          THEN PREOBJIMG
          END tipo 3. ==
          THEN PREOBJCABECALHO
          END tipo 4. ==
          THEN PREOBJREGUA
          END
        END \<-n j - 2. DISP
      NEXT \<-largura R\->B \<-altura R\->B BLANK PICT STO "Montando..." 1. DISP 0. \<-n 1. -
      FOR j \<-n j - 2. DISP
        IF \<-n j - ROLL OBJ\-> DROP
        THEN \-> h
          \<< \<-largura R\->B h R\->B BLANK NEG PICT SWAP 0. R\->B \<-y R\->B 2. \->LIST SWAP GOR '\<-y' h 6. + STO+
          \>>
        ELSE 0. 0. \-> lado img eElemento w h
          \<< img SIZE B\->R 'h' STO B\->R 'w' STO
            CASE lado -1. ==
              THEN 0.
              END lado 0. ==
              THEN \<-largura w - 2. / FLOOR
              END lado 1. ==
              THEN \<-largura w -
              END
            END R\->B \<-y R\->B 2. \->LIST PICT SWAP img GOR '\<-y' h
            IF eElemento
            THEN 6.
            ELSE 0.
            END + STO+
          \>>
        END
      NEXT
    \>>
  \>>
  BUSCAR
  \<< { } \-> string opcoes
    \<< 1. \<-strings SIZE
      FOR j
        IF '\<-strings' j GET string POS
        THEN 'opcoes' "P\225gina " j + j 2. \->LIST 1. \->LIST STO+
        END
      NEXT
      IF opcoes SIZE
      THEN
        WHILE "Resultados" opcoes 1. CLLCD CHOOSE
        REPEAT GETPAGINACOMPILADA
        END
      ELSE "Nada encontrado" MSGBOX
      END
    \>>
  \>>
  ABRIRLIVRO
  \<< OBJ\-> DROP 0. { } { } \-> versao \<-nome opcoes \<-indice \<-paginas \<-anexos \<-strings n \<-posIndice \<-cache
    \<<
      IF versao 1. \=/
      THEN "Vers\227o incompat\237vel" DOERR
      END \<-paginas SIZE 'n' STO 1. n
      START 0.
      NEXT n \->LIST '\<-cache' STO MONTARINDICERAIZ
    \>>
  \>>
  SUBIRINDICE
  \<< \<-posIndice TAIL DUP '\<-posIndice' STO
    IF SIZE
    THEN MONTARINDICE
    ELSE MONTARINDICERAIZ
    END
  \>>
  PREOBJTXT
  \<< SWAP \-> alinhamento
    \<< OBJ\-> DUP 1. - '\<-n' STO+ 1. SWAP
      FOR j 1. \->GROB DUP SIZE B\->R '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO alinhamento SWAP 1. j == 0. 4. \->LIST \<-n ROLLD
      NEXT 6. '\<-altura' STO+
    \>>
  \>>
  PREOBJREGUA
  \<< DUP 6. + '\<-altura' STO+ 1. 2. \->LIST \<-n ROLLD
  \>>
  PREOBJIMG
  \<< DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 0. SWAP 1. 0. 4. \->LIST \<-n ROLLD
  \>>
  PREOBJEQ
  \<<
    IFERR STR\->
    THEN "'" SWAP + "'" +
      IFERR STR\->
      THEN DROP "[\019\002\019Erro na equa\231\227o\019\002\019]" 2. \->GROB
      ELSE 0. \->GROB
      END
    ELSE 0. \->GROB
    END DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 1. 0. 4. \->LIST \<-n ROLLD
  \>>
  PREOBJCABECALHO
  \<< SWAP DROP 2. \->GROB DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 1. 0. 4. \->LIST \<-n ROLLD
  \>>
END
'TEMPLIVRO' PGDIR
'TEMPLIVRO' STO
TEMPLIVRO
ABRIRLIVRO
UPDIR
'TEMPLIVRO' PGDIR
\>>