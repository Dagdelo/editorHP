%%HP: T(3)A(R)F(.);
DIR
  $TITLE "Biblioteca"
  $EXTPRG Apps
  $ROMID 937
  $VISIBLE { SalvarLivro }
  $HIDDEN { Apps Biblioteca AbrirPasta ExcluirLivros ReceberLivro ABRIRLIVRO ENVIAR COMPILARPAGINA GETPAGINACOMPILADA GVIEW MONTARANEXOS MONTARINDICE MONTARINDICERAIZ PREOBJCABECALHO PREOBJEQ PREOBJIMG PREOBJREGUA PREOBJTXT SUBIRINDICE }
  $CONFIG 0
  Biblioteca
  \<< AbrirPasta \-> path
    \<< "Biblioteca" Nomes { { "\>> Remover livros" 0. } { "\>> Receber livro" 1. } } +
      IF 1. CLLCD CHOOSE
      THEN
        CASE DUP TYPE 2. ==
          THEN Nomes SWAP POS 'Dados' SWAP GET
            IFERR ABRIRLIVRO
            THEN path EVAL ERRN DOERR
            END
          END DUP 0. ==
          THEN DROP ExcluirLivros Biblioteca
          END 1. ==
          THEN ReceberLivro Biblioteca
          END
        END
      END path EVAL
    \>>
  \>>
  Apps
  \<<
    IF DUP 0 R~SB ==
    THEN SWAP DUP SIZE 1. + PUSH -49 CF -50 CF \->STR POP "Biblioteca" +
      \<< Biblioteca
      \>> 2. \->LIST 1. \->LIST + SWAP
    END
  \>>
  AbrirPasta
  \<< PATH HOME
    IF 'Livros' VTYPE -1 ==
    THEN Livros CRDIR
    END Livros
    IF 'Nomes' VTYPE -1 == 'Dados' VTYPE -1 == OR
    THEN { } 'Nomes' STO { } 'Dados' STO
    END
  \>>
  ABRIRLIVRO
  \<< OBJ\-> DROP 0. { } { } \-> \<-versao \<-nome \<-opcoes \<-indice \<-paginas \<-anexos \<-strings n \<-posIndice \<-cache
    \<<
      IF \<-versao 1. \=/
      THEN "Vers\227o incompat\237vel" DOERR
      END \<-paginas SIZE 'n' STO 1. n
      START 0.
      NEXT n \->LIST '\<-cache' STO 1. MONTARINDICERAIZ
    \>>
  \>>
  SUBIRINDICE
  \<< \<-posIndice DUP HEAD SWAP TAIL DUP '\<-posIndice' STO
    IF SIZE
    THEN MONTARINDICE
    ELSE MONTARINDICERAIZ
    END
  \>>
  SalvarLivro
  \<< AbrirPasta Nomes \<-nome POS
    IF DUP
    THEN DUP 'Nomes' SWAP \<-nome PUT 'Dados' SWAP \<-versao \<-nome \<-opcoes \<-indice \<-paginas \<-anexos \<-strings 7. \->LIST PUT
    ELSE DROP \<-nome 'Nomes' STO+ \<-versao \<-nome \<-opcoes \<-indice \<-paginas \<-anexos \<-strings 7. \->LIST 1. \->LIST 'Dados' STO+
    END EVAL
  \>>
  ReceberLivro
  \<< 'TEMP' PURGE
    IFERR RCLF -33. SF -35. SF RECV TEMP OBJ\-> DROP \-> nomeLivro dado
      \<< Nomes nomeLivro POS
        IF DUP
        THEN DUP 'Nomes' SWAP nomeLivro PUT 'Dados' SWAP dado PUT
        ELSE DROP nomeLivro 'Nomes' STO+ dado 1. \->LIST 'Dados' STO+
        END nomeLivro " recebido" + MSGBOX
      \>>
    THEN
    END STOF 'TEMP' PURGE
  \>>
  PREOBJTXT
  \<< SWAP \-> alinhamento
    \<< OBJ\-> DUP 1. - '\<-n' STO+ 1. SWAP
      FOR j 1. \->GROB DUP SIZE B\->R '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO alinhamento SWAP 1. j == 0. 4. \->LIST \<-n ROLLD
      NEXT 6. '\<-altura' STO+
    \>>
  \>>
  PREOBJREGUA
  \<< DUP 6. + '\<-altura' STO+ 1. 2. \->LIST \<-n ROLLD
  \>>
  PREOBJIMG
  \<< DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 0. SWAP 1. 0. 4. \->LIST \<-n ROLLD
  \>>
  PREOBJEQ
  \<<
    IFERR STR\->
    THEN "'" SWAP + "'" +
      IFERR STR\->
      THEN DROP "[\019\002\019Erro na equa\231\227o\019\002\019]" 2. \->GROB
      ELSE 0. \->GROB
      END
    ELSE 0. \->GROB
    END DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 1. 0. 4. \->LIST \<-n ROLLD
  \>>
  PREOBJCABECALHO
  \<< SWAP DROP 2. \->GROB DUP SIZE B\->R 6. + '\<-altura' STO+ B\->R \<-largura MAX '\<-largura' STO 1. 0. 4. \->LIST \<-n ROLLD
  \>>
  PreCompilar
  \<< 0 '$CONFIG' STO { Apps Biblioteca AbrirPasta ExcluirLivros ReceberLivro ABRIRLIVRO ENVIAR COMPILARPAGINA GETPAGINACOMPILADA GVIEW MONTARANEXOS MONTARINDICE MONTARINDICERAIZ PREOBJCABECALHO PREOBJEQ PREOBJIMG PREOBJREGUA PREOBJTXT SUBIRINDICE } '$HIDDEN' STO { SalvarLivro } '$VISIBLE' STO 937 '$ROMID' STO 'Apps' '$EXTPRG' STO "Biblioteca" '$TITLE' STO
  \>>
  MONTARINDICERAIZ
  \<< \<-indice 1.
    \<< 1. GET NSUB 2. \->LIST
    \>> DOSUBS
    IF \<-anexos SIZE
    THEN { { "\>> Anexos" 0. } } +
    END { { "\>> Enviar livro" -1. } } + \<-nome SWAP ROT
    IF CLLCD CHOOSE
    THEN
      IF DUP -1. ==
      THEN DROP ENVIAR 1. MONTARINDICERAIZ
      ELSE
        IF DUP
        THEN '\<-posIndice' STO+ 2. MONTARINDICE
        ELSE DROP MONTARANEXOS
        END
      END
    END
  \>>
  MONTARINDICE
  \<< 0. 0. { } \-> ini subindice n lista
    \<< \<-indice 'subindice' STO \<-posIndice OBJ\-> 1. SWAP
      START subindice SWAP GET 'subindice' STO
      NEXT subindice OBJ\-> 2. - 'n' STO
      IF
      THEN SWAP DROP GETPAGINACOMPILADA SUBIRINDICE
      ELSE 1. n
        FOR j 1. GET n 2. + j - 2. \->LIST 1. \->LIST 'lista' STO+
        NEXT { { ".." 0. } } 'lista' STO+
        IF lista ini CLLCD CHOOSE
        THEN
          IF DUP
          THEN '\<-posIndice' STO+ 2. MONTARINDICE
          ELSE DROP SUBIRINDICE
          END
        END
      END
    \>>
  \>>
  MONTARANEXOS
  \<<
    IF "Anexos" \<-anexos 0. CLLCD CHOOSE
    THEN
      IFERR STR\->
      THEN "Formato inv\225lido" MSGBOX
      END
    ELSE 1. MONTARINDICERAIZ
    END
  \>>
  GVIEW
  \<< DUP SIZE # 0h # 0h 1. \-> img mX mY x y loop
    \<< img \->LCD
      WHILE loop
      REPEAT
        IFERR 0. WAIT
        THEN 0. 'loop' STO
        END
        CASE DUP 25.1 ==
          THEN
            IF y # 28h \>=
            THEN y # 28h - 'y' STO
            END
          END DUP 25.3 ==
          THEN # 0h 'y' STO
          END DUP 36.1 ==
          THEN
            IF x # 42h + mX <
            THEN x # 42h + 'x' STO
            END
          END DUP 36.3 ==
          THEN
            IF mX # 42h <
            THEN # 0h
            ELSE mX # 42h -
            END 'x' STO
          END DUP 35.1 ==
          THEN
            IF y # 28h + mY <
            THEN y # 28h + 'y' STO
            END
          END DUP 35.3 ==
          THEN
            IF mY # 28h <
            THEN # 0h
            ELSE mY # 28h -
            END 'y' STO
          END DUP 34.1 ==
          THEN
            IF x # 42h \>=
            THEN x # 42h - 'x' STO
            END
          END DUP 34.3 ==
          THEN # 0h 'x' STO
          END
        END DROP img x y 2. \->LIST mX mY 2. \->LIST SUB \->LCD
      END
    \>>
  \>>
  GETPAGINACOMPILADA
  \<<
    IF DUP 0. ==
    THEN DROP "\205ndice inv\225lido" MSGBOX
    ELSE '\<-cache' OVER GET
      IF DUP 0. ==
      THEN DROP '\<-paginas' OVER GET COMPILARPAGINA PICT RCL DUP ROT SWAP '\<-cache' UNROT PUT
      ELSE SWAP DROP
      END DUP PICT STO GVIEW
    END
  \>>
  ExcluirLivros
  \<< "Excluir" { { "\<< Voltar" 0. } } Nomes +
    IF 1. CLLCD CHOOSE
    THEN
      IF DUP 0. \=/
      THEN Nomes SWAP POS Nomes SIZE \-> n len
        \<< Nomes OBJ\-> DROP len n - 1. + ROLL DROP len 1. - \->LIST 'Nomes' STO Dados OBJ\-> DROP len n - 1. + ROLL DROP len 1. - \->LIST 'Dados' STO
        \>> ExcluirLivros
      ELSE DROP
      END
    END
  \>>
  ENVIAR
  \<< \<-nome \<-versao \<-nome \<-opcoes \<-indice \<-paginas \<-anexos \<-strings 7. \->LIST 2. \->LIST 'TEMP' STO RCLF -33. SF -35. SF
    IFERR 'TEMP' SEND
    THEN DROP
    END STOF 'TEMP' PURGE
  \>>
  COMPILARPAGINA
  \<< 0. 131. 0. 0. 0. \-> \<-n \<-largura tipo \<-altura \<-y
    \<< OBJ\-> '\<-n' STO "Gerando..." 1. DISP 1. \<-n
      FOR j OBJ\-> DROP 'tipo' STO
        CASE tipo 0. ==
          THEN PREOBJTXT
          END tipo 1. ==
          THEN PREOBJEQ
          END tipo 2. ==
          THEN PREOBJIMG
          END tipo 3. ==
          THEN PREOBJCABECALHO
          END tipo 4. ==
          THEN PREOBJREGUA
          END
        END \<-n j - 2. DISP
      NEXT \<-largura R\->B \<-altura R\->B BLANK PICT STO "Montando..." 1. DISP 0. \<-n 1. -
      FOR j \<-n j - 2. DISP
        IF \<-n j - ROLL OBJ\-> DROP
        THEN \-> h
          \<< \<-largura R\->B h R\->B BLANK NEG PICT SWAP 0. R\->B \<-y R\->B 2. \->LIST SWAP GOR '\<-y' h 6. + STO+
          \>>
        ELSE 0. 0. \-> lado img eElemento w h
          \<< img SIZE B\->R 'h' STO B\->R 'w' STO
            CASE lado -1. ==
              THEN 0.
              END lado 0. ==
              THEN \<-largura w - 2. / FLOOR
              END lado 1. ==
              THEN \<-largura w -
              END
            END R\->B \<-y R\->B 2. \->LIST PICT SWAP img GOR '\<-y' h
            IF eElemento
            THEN 6.
            ELSE 0.
            END + STO+
          \>>
        END
      NEXT
    \>>
  \>>
  Compilar
  \<< CRLIB { $TITLE $EXTPRG $ROMID $VISIBLE $HIDDEN $CONFIG Compilar Biblioteca Apps SalvarLivro AbrirPasta ExcluirLivros ReceberLivro ABRIRLIVRO ENVIAR COMPILARPAGINA GETPAGINACOMPILADA GVIEW MONTARANEXOS MONTARINDICE MONTARINDICERAIZ PREOBJCABECALHO PREOBJEQ PREOBJIMG PREOBJREGUA PREOBJTXT SUBIRINDICE } PURGE :1: 937 PURGE DUP :1: 937 STO 'EditorHP' STO PUSH HOME 937 ATTACH POP
    \<< PUSH HOME 937 DETACH POP 'Desinstalar' PURGE
    \>> 'Desinstalar' STO
  \>>
END
